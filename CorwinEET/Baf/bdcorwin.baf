IF
Global("EECorwinScar","Global",1)
!See("Scar")
See([PC])
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogNoSet(Player1)
END

IF
AreaCheck("BG0128")
Global("EECorDpl","BG0128",1) 
Global("EECorDpl","BG0603",2)
THEN
RESPONSE #100
SetGlobal("EECorDpl","BG0128",2)
END

IF
AreaCheck("BG0603")
Global("EECorDpl","BG0128",2) 
Global("EECorDpl","BG0603",1)
THEN
RESPONSE #100
SetGlobal("EECorDpl","BG0603",2)
END

IF
Global("EECorwinScar","Global",2)
AreaCheck("bg1300")
See("Begbax_C")
Global("EEOwnHouse","Locals",0)
THEN
RESPONSE #100
SetGlobal("EEOwnHouse","Locals",1)
END

IF
Global("EEMetRohma","Global",1)
AreaCheck("bg1317")
See("BDaudam")
THEN
RESPONSE #100
FaceObject("BDrohma")
SetGlobal("EEMetRohma","Global",2)
END

IF
AreaType(FOREST)
Global("EECorwoodland","LOCALS",0)
Delay(30)
THEN
RESPONSE #100
SetGlobal("EECorwoodland","LOCALS",1)
END

IF
OR(2)
AreaCheck("bg0100")
AreaCheck("bg0300")
Global("EECorFriend","Locals",0)
Delay(45)
THEN
RESPONSE #100
SetGlobal("EECorFriend","Locals",1)
END

IF
RealGlobalTimerExpired("EECorBanT","Locals")
!AreaType(DUNGEON)
OR(2)
Global("EECorFriend","Locals",2)
Global("EECorFriend","Locals",4)
THEN
RESPONSE #100
IncrementGlobal("EECorFriend","Locals",1)
END

IF
GlobalGT("Chapter","Global",5)
OR(16)
AreaCheck("BG4801")
AreaCheck("BG2616")
AreaCheck("BG3351")
AreaCheck("BG3304")
AreaCheck("BG2301")
AreaCheck("BG0105")
AreaCheck("BG0116")
AreaCheck("BG0119")
AreaCheck("BG0706")
AreaCheck("BG4003")
AreaCheck("BG1001")
AreaCheck("bh0115")
AreaCheck("bh0107")
AreaCheck("BG3357")
AreaCheck("BG0114")
AreaCheck("CVRoa2")
Global("bd_drinks_corwin","global",0)
THEN
RESPONSE #100
SetGlobal("bd_drinks_corwin","global",1)
END

IF
OR(11)
Global("EEOwnHouse","Locals",1)
Global("EEMetRohma","Global",2)
Global("EECorDpl","BG0603",1)
Global("EECorDpl","BG0128",1)
Global("EECorwoodland","LOCALS",1)
Global("EECorFriend","Locals",1)
Global("EECorFriend","Locals",3)
Global("EECorFriend","Locals",5)
Global("EECorBeno","bg0224",1)
Global("EECorBeno","bg0224",5)
Global("bd_drinks_corwin","global",1)
InParty(Myself)
!See([ENEMY])
CombatCounter(0)
!StateCheck(Player1,CD_STATE_NOTVALID) 
See(Player1)
THEN
RESPONSE #100
StartDialogNoSet(Player1)
END

IF
HPLT(Myself,11)
HasItem("EEAmul",Myself)
THEN
RESPONSE #100
ReallyForceSpellRES("BGSleep",Myself) 
Wait(3)
ReallyForceSpell(Myself,HEAL_NO_VISUAL) 
END

IF
!Global("EECorwinBG1","Global",0)
AreaCheck("BD1000")
HasItem("EEAmul",Myself) 
THEN
RESPONSE #100
DestroyItem("EEAmul") 
END