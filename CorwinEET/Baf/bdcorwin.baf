IF
Global("EECorwinScar","Global",1)
!See("Scar")
See([PC])
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogNoSet(Player1)
END

IF
AreaCheck("BG0128")
Global("EECorDpl","BG0128",1) 
Global("EECorDpl","BG0603",2)
THEN
RESPONSE #100
SetGlobal("EECorDpl","BG0128",2)
END

IF
AreaCheck("BG0603")
Global("EECorDpl","BG0128",2) 
Global("EECorDpl","BG0603",1)
THEN
RESPONSE #100
SetGlobal("EECorDpl","BG0603",2)
END

IF
Global("EECorwinScar","Global",2)
AreaCheck("bg1300")
OR(2)
See("Begbax_C")  
See("Door0316")
Global("EEOwnHouse","Locals",0)
THEN
RESPONSE #100
SetGlobal("EEOwnHouse","Locals",1)
END

IF
Global("EEMetRohma","Global",1)
AreaCheck("bg1317")
See("BDaudam")
THEN
RESPONSE #100
FaceObject("BDrohma")
SetGlobal("EEMetRohma","Global",2)
END

IF
AreaType(FOREST)
Global("EECorwoodland","LOCALS",0)
Delay(30)
THEN
RESPONSE #100
SetGlobal("EECorwoodland","LOCALS",1)
END

IF
OR(2)
AreaCheck("bg0100")
AreaCheck("bg0300")
Global("EECorFriend","Locals",0)
Delay(45)
THEN
RESPONSE #100
SetGlobal("EECorFriend","Locals",1)
END

IF
Global("EECorFriend","Locals",2)
GlobalGT("EEMetRohma","Global",2)
RealGlobalTimerExpired("EECorBanT","Locals")
THEN
RESPONSE #100
SetGlobal("EECorFriend","Locals",3)
RealSetGlobalTimer("EECorBanT","Locals",1800)
END

IF
RealGlobalTimerExpired("EECorBanT","Locals")
!AreaType(DUNGEON)
OR(2)
Global("EECorFriend","Locals",3)
Global("EECorFriend","Locals",5)
THEN
RESPONSE #100
IncrementGlobal("EECorFriend","Locals",1)
END

IF
Global("EECorAnm","BG4800",0)
AreaCheck("BG4800")
Delay(36)
THEN
RESPONSE #100
SetGlobal("EECorAnm","BG4800",1)
END

IF
AreaCheck("BG0112")
Global("EECorUnderc","BG0112",0)
See("Harlot")
GlobalLT("Chapter","Global",7)
THEN
RESPONSE #100
SetGlobal("EECorUnderc","BG0112",1)
END

IF
Global("Scarmission","Global",3)
AreaCheck("BG1100")
Global("EECorMoreTaskSC","bg1100",0)
Delay(12)
THEN
RESPONSE #100
SetGlobal("EECorMoreTaskSC","bg1100",1)
END

IF
AreaCheck("BG2611")
OR(3)
Global("EEBendalCorw2","Global",1)
Global("EEBendalCorw3","Global",1)
Global("EEBendalCorw4","Global",1)
Global("EECorwShista","BG2611",1)
THEN
RESPONSE #100
SetGlobal("EEBendalCorw2","Global",2)
SetGlobal("EECorwShista","BG2611",2)
END

IF
GlobalGT("Chapter","Global",6)
OR(16)
AreaCheck("BG4801")
AreaCheck("BG2616")
AreaCheck("BG3351")
AreaCheck("BG3304")
AreaCheck("BG2301")
AreaCheck("BG0105")
AreaCheck("BG0116")
AreaCheck("BG0119")
AreaCheck("BG0706")
AreaCheck("BG4003")
AreaCheck("BG1001")
AreaCheck("bh0115")
AreaCheck("bh0107")
AreaCheck("BG3357")
AreaCheck("BG0114")
AreaCheck("CVRoa7")
Global("bd_drinks_corwin","global",0)
THEN
RESPONSE #100
SetGlobal("bd_drinks_corwin","global",1)
END

IF
AreaCheck("BG1200")
GlobalLT("ScarMission","GLOBAL",5)
Global("EECorThNotYet","BG1200",0)
OR(3)
Range("Info0616b",15)
Range("DOOR0611",15)
Range("Door0616",15)
THEN
RESPONSE #100
SetGlobal("EECorThNotYet","BG1200",1)
END

IF
Global("EECorIrThr","LOCALS",0)
AreaCheck("BG0615")
Dead("ZHALIMAR")
Dead("aasim")
Dead("Alai")
Dead("Naaman")
Delay(16)
THEN
RESPONSE #100
SetGlobal("EECorIrThr","LOCALS",1)
END

IF
Global("Chapter","Global",7)
Global("EERescEltan","Locals",0)
PartyHasItem("Misc55")
THEN
RESPONSE #100
SetGlobal("EERescEltan","Locals",1)
END

IF
Global("BD_SPEAKER_THREAT","BD0120",2)
Global("EECorwAfter","BD0120",0)
Delay(12)
!See([ENEMY])
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("EECorwAfter","BD0120",1)
END

IF
Global("Chapter","Global",10)
GlobalGT("EERescEltan","Locals",1)
Global("EECorwBelievePC","Global",0)
InParty(Myself)
THEN
RESPONSE #100
SetGlobal("EECorwBelievePC","Global",1)
END

IF
AreaCheck("BD0120")
OR(2)
Dead("bdkorlas") 
Global("BD_KORLASZ_SURRENDER","GLOBAL",1)
GlobalLT("EECorwAfter","BD0120",3)
Delay(12)
!See([ENEMY])
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("EECorwAfter","BD0120",3)
END

IF
OR(20)
Global("EEOwnHouse","Locals",1)
Global("EEMetRohma","Global",2)
Global("EECorDpl","BG0603",1)
Global("EECorDpl","BG0128",1)
Global("EECorwoodland","LOCALS",1)
Global("EECorFriend","Locals",1)
Global("EECorFriend","Locals",4)
Global("EECorFriend","Locals",6)
Global("EECorBeno","bg0224",1)
Global("EECorBeno","bg0224",5)
Global("bd_drinks_corwin","global",1)
Global("EECorAnm","BG4800",1)
Global("EECorUnderc","BG0112",1)
Global("EECorThNotYet","BG1200",1)
Global("EECorMoreTaskSC","bg1100",1)
Global("EECorIrThr","LOCALS",1)
Global("EEBendalCorw2","Global",2)
Global("EERescEltan","Locals",1)
Global("EECorwAfter","BD0120",1)
Global("EECorwAfter","BD0120",3)
InParty(Myself)
!See([ENEMY])
CombatCounter(0)
!StateCheck(Player1,CD_STATE_NOTVALID) 
See(Player1)
THEN
RESPONSE #100
StartDialogNoSet(Player1)
END

IF
Global("EECorwPrison","BG0607",1)
THEN
RESPONSE #100
Dialogue("Corwin")
END

IF
HPLT(Myself,11)
HasItem("EEAmul",Myself)
THEN
RESPONSE #100
ReallyForceSpellRES("BGSleep",Myself) 
Wait(3)
ReallyForceSpell(Myself,HEAL_NO_VISUAL) 
END

IF
!Global("EECorwinBG1","Global",0)
AreaCheck("BD1000")
HasItem("EEAmul",Myself) 
THEN
RESPONSE #100
DestroyItem("EEAmul") 
END