IF
Global("EECorwinScar","Global",1)
!See("Scar")
See([PC])
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogNoSet(Player1)
END

IF
AreaCheck("%SWBaldursGate_MerchantLeague_L2%")
Global("EECorDpl","%SWBaldursGate_MerchantLeague_L2%",1) 
Global("EECorDpl","Global",1) 
THEN
RESPONSE #100
SetGlobal("EECorDpl","%SWBaldursGate_MerchantLeague_L2%",2)
END

IF
AreaCheck("%SWBaldursGate_SevenSuns_Cellar%")
Global("EECorDpl","Global",1) 
Global("EECorDpl","%SWBaldursGate_SevenSuns_Cellar%",1)
THEN
RESPONSE #100
SetGlobal("EECorDpl","%SWBaldursGate_SevenSuns_Cellar%",2)
END

IF
Global("EECorwinScar","Global",2)
AreaCheck("%SEBaldursGate%")
OR(2)
See("Begbax_C")  
See("Door0316")
Global("EEOwnHouse","Locals",0)
THEN
RESPONSE #100
SetGlobal("EEOwnHouse","Locals",1)
END

IF
Global("EEMetRohma","Global",1)
AreaCheck("%SEBaldursGate_TaxeksHouse_L2%")
See("BDaudam")
THEN
RESPONSE #100
FaceObject("BDrohma")
SetGlobal("EEMetRohma","Global",2)
END

IF
AreaType(FOREST)
Global("EECorwoodland","LOCALS",0)
Delay(30)
THEN
RESPONSE #100
SetGlobal("EECorwoodland","LOCALS",1)
END

IF
OR(2)
AreaCheck("%NWBaldursGate%")
AreaCheck("%NEBaldursGate%")
Global("EECorFriend","Locals",0)
Delay(45)
THEN
RESPONSE #100
SetGlobal("EECorFriend","Locals",1)
END

IF
Global("EECorFriend","Locals",2)
GlobalGT("EEMetRohma","Global",2)
RealGlobalTimerExpired("EECorBanT","Locals")
THEN
RESPONSE #100
SetGlobal("EECorFriend","Locals",3)
RealSetGlobalTimer("EECorBanT","Locals",1800)
END

IF
RealGlobalTimerExpired("EECorBanT","Locals")
!AreaType(DUNGEON)
OR(2)
Global("EECorFriend","Locals",3)
Global("EECorFriend","Locals",5)
THEN
RESPONSE #100
IncrementGlobal("EECorFriend","Locals",1)
END

IF
Global("EECorAnm","%Nashkel%",0)
AreaCheck("%Nashkel%")
Delay(36)
THEN
RESPONSE #100
SetGlobal("EECorAnm","%Nashkel%",1)
END

IF
AreaCheck("%BaldursGate_Undercellars%")
Global("EECorUnderc","%BaldursGate_Undercellars%",0)
OR(2)
See("Harlot")
See("Proslay")
GlobalLT("Chapter","Global",7)
THEN
RESPONSE #100
SetGlobal("EECorUnderc","%BaldursGate_Undercellars%",1)
END

IF
Global("Scarmission","Global",3)
AreaCheck("%SWBaldursGate%")
Global("EECorMoreTaskSC","%SWBaldursGate%",0)
Delay(12)
THEN
RESPONSE #100
SetGlobal("EECorMoreTaskSC","%SWBaldursGate%",1)
END

IF
AreaCheck("%Candlekeep_Library_L4%")
OR(3)
Global("EEBendalCorw2","Global",1)
Global("EEBendalCorw3","Global",1)
Global("EEBendalCorw4","Global",1)
Global("EECorwShista","%Candlekeep_Library_L4%",1)
THEN
RESPONSE #100
SetGlobal("EEBendalCorw2","Global",2)
SetGlobal("EECorwShista","%Candlekeep_Library_L4%",2)
END

IF
GlobalGT("Chapter","Global",6)
OR(16)
AreaCheck("%Nashkel_Inn%")
AreaCheck("%Candlekeep_Inn_L1%")
AreaCheck("%Beregost_FeldepostsInn_L1%")
AreaCheck("%Beregost_JovialJuggler_L1%")
AreaCheck("%FriendlyArmInn_L1%")
AreaCheck("%SEBaldursGate_Blade&Stars_L1%")
AreaCheck("%NWBaldursGate_Helm&Cloak_L1%")
AreaCheck("%NBaldursGate_ThreeOldKegs_L1%")
AreaCheck("%EBaldursGate_ElfsongTavern_L2%")
AreaCheck("%Gullykin_Winery_L1%")
AreaCheck("%UlgothsBeard_Inn%")
AreaCheck("bh0115")
AreaCheck("bh0107")
AreaCheck("%Beregost_RedSheaf_L1%")
AreaCheck("%NEBaldursGate_BlushingMermaid_L1%")
AreaCheck("CVRoa7")
Global("bd_drinks_corwin","global",0)
THEN
RESPONSE #100
SetGlobal("bd_drinks_corwin","global",1)
END

IF
AreaCheck("%BaldursGateDocks%")
GlobalLT("ScarMission","GLOBAL",5)
Global("EECorThNotYet","%BaldursGateDocks%",0)
OR(3)
Range("Info0616b",15)
Range("DOOR0611",15)
Range("Door0616",15)
THEN
RESPONSE #100
SetGlobal("EECorThNotYet","%BaldursGateDocks%",1)
END

IF
Global("EECorIrThr","LOCALS",0)
AreaCheck("%BaldursGateDocks_IronThrone_L5%")
Dead("ZHALIMAR")
Dead("aasim")
Dead("Alai")
Dead("Naaman")
Delay(16)
THEN
RESPONSE #100
SetGlobal("EECorIrThr","LOCALS",1)
END

IF
Global("Chapter","Global",7)
Global("EERescEltan","Locals",0)
PartyHasItem("Misc55")
THEN
RESPONSE #100
SetGlobal("EERescEltan","Locals",1)
END

IF
Global("BD_SPEAKER_THREAT","BD0120",2)
Global("EECorwAfter","BD0120",0)
Delay(12)
!See([ENEMY])
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("EECorwAfter","BD0120",1)
END

IF
Global("Chapter","Global",10)
GlobalGT("EERescEltan","Locals",1)
Global("EECorwBelievePC","Global",0)
InParty(Myself)
THEN
RESPONSE #100
SetGlobal("EECorwBelievePC","Global",1)
END

IF
AreaCheck("BD0120")
OR(2)
Dead("bdkorlas") 
Global("BD_KORLASZ_SURRENDER","GLOBAL",1)
GlobalLT("EECorwAfter","BD0120",3)
Delay(12)
!See([ENEMY])
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("EECorwAfter","BD0120",3)
END

IF
OR(20)
Global("EEOwnHouse","Locals",1)
Global("EEMetRohma","Global",2)
Global("EECorDpl","%SWBaldursGate_SevenSuns_Cellar%",1)
Global("EECorDpl","%SWBaldursGate_MerchantLeague_L2%",1)
Global("EECorwoodland","LOCALS",1)
Global("EECorFriend","Locals",1)
Global("EECorFriend","Locals",4)
Global("EECorFriend","Locals",6)
Global("EECorBeno","%WSewers%",1)
Global("EECorBeno","%WSewers%",5)
Global("bd_drinks_corwin","global",1)
Global("EECorAnm","%Nashkel%",1)
Global("EECorUnderc","%BaldursGate_Undercellars%",1)
Global("EECorThNotYet","%BaldursGateDocks%",1)
Global("EECorMoreTaskSC","%SWBaldursGate%",1)
Global("EECorIrThr","LOCALS",1)
Global("EEBendalCorw2","Global",2)
Global("EERescEltan","Locals",1)
Global("EECorwAfter","BD0120",1)
Global("EECorwAfter","BD0120",3)
InParty(Myself)
!See([ENEMY])
CombatCounter(0)
!StateCheck(Player1,CD_STATE_NOTVALID) 
See(Player1)
THEN
RESPONSE #100
StartDialogNoSet(Player1)
END

IF
Global("EECorwPrison","%SWBaldursGate_FlamingFistHQ_L1%",1)
THEN
RESPONSE #100
Dialogue("Corwin")
END

IF
HPLT(Myself,11)
HasItem("EEAmul",Myself)
THEN
RESPONSE #100
ReallyForceSpellRES("BGSleep",Myself)
Wait(3)
ReallyForceSpell(Myself,HEAL_NO_VISUAL) 
END

IF
!Global("EECorwinBG1","Global",0)
AreaCheck("BD1000")
HasItem("EEAmul",Myself) 
THEN
RESPONSE #100
DestroyItem("EEAmul") 
END